#
# qmake configuration for Clang on OS X
#

MAKEFILE_GENERATOR = UNIX
TARGET_PLATFORM = macx
TEMPLATE = app
CONFIG += qt warn_on release app_bundle incremental global_init_link_order lib_version_first plugin_no_soname link_prl
QT += core gui
QMAKE_INCREMENTAL_STYLE = sublib

include(mac.conf)
include(gcc-base-macx.conf)
include(clang.conf)

SDK_PATH = $$(SDK_ROOT)
isEmpty( SDK_PATH ) : error("Please set SDK_ROOT environment variable, usually like /Applications/Xcode.app/Contents/Developer/Platforms/MacOSX.platform/Developer/SDKs/MacOSX10.8.sdk")

QMAKE_OBJCFLAGS_PRECOMPILE       = -x objective-c-header -c ${QMAKE_PCH_INPUT} -o ${QMAKE_PCH_OUTPUT}
QMAKE_OBJCFLAGS_USE_PRECOMPILE   = $$QMAKE_CFLAGS_USE_PRECOMPILE
QMAKE_OBJCXXFLAGS_PRECOMPILE     = -x objective-c++-header -c ${QMAKE_PCH_INPUT} -o ${QMAKE_PCH_OUTPUT}
QMAKE_OBJCXXFLAGS_USE_PRECOMPILE = $$QMAKE_CFLAGS_USE_PRECOMPILE

QMAKE_CFLAGS                 += --sysroot=$$SDK_PATH
QMAKE_CFLAGS                 += -isystem $$SDK_PATH/usr/include
QMAKE_CFLAGS                 += -iwithsysroot $$SDK_PATH
QMAKE_CXXFLAGS               += --sysroot=$$SDK_PATH
QMAKE_CXXFLAGS               += -isystem $$SDK_PATH/usr/include
QMAKE_CXXFLAGS               += -iwithsysroot $$SDK_PATH
QMAKE_OBJECTIVE_CFLAGS   += --sysroot=$$SDK_PATH -isystem $$SDK_PATH/usr/include -iwithsysroot $$SDK_PATH
QMAKE_LFLAGS                 += "-Wl,-syslibroot $$SDK_PATH"

QMAKE_MOC = $$[QT_INSTALL_BINS]/moc -DBOOST_TT_HAS_OPERATOR_HPP_INCLUDED

load(qt_config)
